@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/fictionTargets.cmake")

# Helper macro to define imported interface library
macro(fiction_define_imported_target target_name include_subdir)
    if(NOT TARGET ${target_name})
        add_library(${target_name} INTERFACE IMPORTED)
        set_target_properties(${target_name} PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${PACKAGE_PREFIX_DIR}/include/${include_subdir}"
        )
    endif()
endmacro()

# Define dependencies
fiction_define_imported_target(fiction::alice "")
fiction_define_imported_target(fiction::mockturtle "")
fiction_define_imported_target(fiction::nlohmann_json "")
fiction_define_imported_target(fiction::graph-coloring "graph-coloring")
fiction_define_imported_target(fiction::undirected_graph "undirected_graph")
fiction_define_imported_target(fiction::combinations "combinations")
fiction_define_imported_target(fiction::tinyxml2 "")

# ALGLIB (Optional)
if(EXISTS "${PACKAGE_PREFIX_DIR}/include/alglib")
    fiction_define_imported_target(fiction::alglib "alglib")
endif()

check_required_components(fiction)
